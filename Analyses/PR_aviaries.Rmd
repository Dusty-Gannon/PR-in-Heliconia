---
title: "Testing for generality of pollinator recognition in *Heliconia*: Analysis of aviary data"
author: "D.G. Gannon, A.S. Hadley, U.G. Kormann, F.A. Jones, M.G. Betts"
header-includes:
  \usepackage{setspace}
  \onehalfspacing
bibliography: /Users/dusty/Documents/zotero_library.bib
output:
  github_document:
    pandoc_args: --webtex
---

### R packages


```{r setup, message=FALSE, warning=FALSE}
knitr::opts_chunk$set(echo = TRUE)

    require(tidyverse)
    require(here)
    require(rstan)
    require(loo)

# User-defined functions
    hours <- function(x){
        substr(x, 1, nchar(x)-2)
    }

    minutes <- function(x){
      substr(x, nchar(x)-2+1, nchar(x))
    }
    
# function to get means on pollen tube scale
   pt_mean <- function(v, h){
     exp(h%*%v)
   }

```

### Load data

```{r data}

# Load data
  av <- read_csv(file = here("Data", "aviaries_data.csv"))

# Sort rows by species, then plant individual ID, then experiment ID
  av <- av[with(av, order(Species, Plant, Experiment)), ]
  
# Create factors for species, treatment, and plant individual
  av$f.species <- as.factor(av$Species)
  av$Treatment2 <- av$Treatment
  av$Treatment2[av$Treatment2 == "RTAH"] <- "SB"
  av$Treatment2[av$Treatment2 == "GREH"] <- "LB"
  
  av$f.trtmnt <- factor(av$Treatment2, levels = c("HP", "SB", "LB"))
  av$f.plant <- factor(av$Plant)
  
```


## Summary

To test whether pollen germination and tube growth is dependent on the identity and morphology of a floral visitor, we conducted pollination experiments with captive hummingbirds inside portable aviaries. In these experiments, we randomly assigned flowers to one of three experimental treatments: 1) hand-pollination only (HP treatment); 2) hand pollination followed by a visit from a pollen-free rufous-tailed hummingbird (SB treatment); 3) hand pollination followed by a visit from a pollen-free long-billed hummingbird (LB treatment).

In all experiments, we used only virgin flowers that had been covered with mesh bags prior to anthesis in order to preclude pollination by free-ranging pollinators. Flowers were not emasculated, however, due to extremely low numbers of pollen tubes in emasculated flowers in natural settings (A. S. Hadley, unpublished data).  Using small, portable  aviaries (1m x 1m x 2m; Figure S1) that we erected around live inflorescences, we standardized the number of pollinator visits to the flowers that were visited by birds (limited to one). Additionally, we standardized the quality of pollen available to flowers that received the control treatment (HP; no interaction with a hummingbird) and those visited by a hummingbird. We did this by hand pollinating two virgin flowers using pollen from the same donor, then randomly assigning one as a control which we covered with a paper sleeve during the experiment. The other was left open to a visit from a captive hummingbird (LB or SB) that we cleaned under 20x magnification prior to releasing into the aviary. We further attempted to reduce variation in the quantity of pollen available to the flowers by applying pollen in an even layer across the stigmatic surface with a toothpick under 20x magnification. The size of Heliconia pollen grains makes it impractical to quantify the number of grains on the stigma in the field, but all hand pollinations were conducted by the same experimenter to limit variability. After the hummingbird visited the treatment flower, we checked the stigma again to ensure that pollen was still present and in an even layer on the stigmatic surface. 


\subsection{Model}

  We assume that pollen tube counts, $y_{i} \in \mathbb{R}^+$, $i=1,2,...,N$, are draws from Poisson distributions with rate parameters $\lambda_{i}$. We model the rate parameter as
  
$$
\begin{split}
\log \lambda_{i} = {\bf X}_{i}{\bm \beta} + \gamma_{p(s)}^{(i)};\\
\\
\gamma_{1,s}, \gamma_{2,s},...,\gamma_{P_s,s} \overset{iid}{\sim} \mathcal{N}(0, \sigma_s^2)
\end{split}
$$

where $P_s$ is the total number of plants of species $s$ used in experiments, $S$ is the number of species tested, and ${\bf X}_{i}$ is the $i^{\text{th}}$ row of the model matrix $\bf X$. We use the subscript $p(s)$ to indicated that the effect of plant $p$ is nested within species $s$, and superscript $(i)$ to be clear that the $i^\text{th}$ style came from plant $p$ of species $s$. In these experiments, we had 3 treatments (hand pollination only, hand pollination followed by a visit from a clean, short-billed hummingbird, and hand pollination followed by a visit from a long-billed hummingbird), and $S=4$ plant species. Thus, $\bf X$ is rank $K=S\times P$.

We placed weakly informative priors on the elements of the $K$-dimensional regression parameter vector $\bm \beta$, assuming ${\bm \beta}_K \sim \mathcal{N}({\bf 0},\ 1.5\cdot {\bf I}_K)$, where ${\bf I}_K$ is the rank $K$ identity matrix, and the species-specific scale parameters such that $\sigma_s \sim \text{half-Normal}(0,1)$ for each $s \in \{1,2,...,S\}$. This structure allows us to account for repeated experiments on individual plants without assuming all plant effects are i.i.d. random variables, but instead that plant effects are i.i.d. within a species.


```{stan output.var="poisson_model", eval=FALSE}

  data{
   
    int N;         //number of observations
    int K;         //number of regressions parameters
    int P;         //number of unique plants sampled (i.e. number of intercepts)
    int S;         //number of species in the analysis
    int pl[N];  //index for the plant in experiment i
    int sp[N];     //index for the plant species in experiment i
    int<lower=0> y[N];  //observations
    matrix[N,K] X;      //design matrix
    //real mu_prior;      // prior mean for grand mean
  
  }
  
  parameters{
  
    vector[K] beta;            //vector of regression parameters
    real gamma_raw[P];         //individual plant effects before scaling
    real<lower=0> sigma[S];    //sd of plant effects, unique across species
  
  }
  
  transformed parameters{
  
    real<lower=0> lambda[N];  //rate parameter for the nth obs.

    for(n in 1:N){
      lambda[n] = exp(X[n,]*beta + gamma_raw[pl[n]]*sigma[sp[n]]);
    }
  
  }
  
  model{
  
    //priors
    //beta[1] ~ normal(mu_prior, 0.5);
    for(k in 1:K){
      beta[k] ~ normal(0,1.5);
    }
    
    for(s in 1:S){
      sigma[s] ~ normal(0,1);
    }
    
    for(p in 1:P){
      gamma_raw[p] ~ normal(0,1);
    }
    
    //model
    for(n in 1:N){
      y[n] ~ poisson(lambda[n]);
    }
    
  }

  generated quantities{
  
    vector[N] loglik;
    int y_rep[N];
    
    for(n in 1:N){
      loglik[n] = poisson_lpmf(y[n] | lambda[n]);
      y_rep[n] = poisson_rng(lambda[n]);
    }
  
  }
  

```
  
  
```{r run m1, echo=FALSE, include=FALSE, eval=FALSE}

# compile data to give to stan

  N <- dim(av)[1]
  y <- av$Tube_Count
  
  X <- model.matrix(~f.species*f.trtmnt -1,
                    data = av)
  K <- dim(X)[2]
  pl <- as.integer(as.factor(av$Plant))
  sp <- as.integer(av$f.species)
  S <- length(unique(sp))
  P <- length(unique(pl))
  
  
  mod.data <- list(N=N, K=K, S=S, P=P,
                   sp=sp, pl=pl,
                   y=y, X=X)
                  #mu_prior=log(grand_mean))
  
  mfit <- sampling(poisson_model, data=mod.data)  
  
# looic and other model assessments
  
  loglik <- extract_log_lik(mfit, "loglik", merge_chains = FALSE)
  
  reff <- relative_eff(exp(loglik))
  
  mfit_loo <- loo(loglik, r_eff = reff)

# save the model fit
  saveRDS(mfit, file = here("Data", "av_model_fit.rds"))
  
```

$~$

************
 
\subsubsection{References}







